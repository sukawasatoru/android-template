/*
 * Copyright 2023, 2024 sukawasatoru
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    alias libs.plugins.android.test
    alias libs.plugins.kotlin.android
}

android {
    namespace 'jp.tinyport.tinyapplication.macrobenchmark'

    targetProjectPath ':app'
    experimentalProperties['android.experimental.self-instrumenting'] = true

    defaultConfig {
        signingConfig signingConfigs.debug
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        benchmark {
            initWith debug
            matchingFallbacks += 'release'
        }
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/debug.p12")
        }
    }
}

androidComponents {
    beforeVariants(selector().all()) {
        enabled = buildType == 'benchmark'
    }
}

dependencies {
    implementation libs.androidx.benchmark
    implementation libs.androidx.test.junit
}
