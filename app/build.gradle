apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def appId = 'jp.tinyport.myapplication'
def providerAuthority = appId

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

android {
    compileSdkVersion android_compile_sdk_version.toInteger()
    buildToolsVersion android_build_tools_version

    defaultConfig {
        applicationId appId
        minSdkVersion android_min_sdk_version.toInteger()
        targetSdkVersion android_target_sdk_version.toInteger()
        versionCode 1
        versionName '1.0'

        jackOptions {
            enabled false
        }

        manifestPlaceholders = [
                providerAuthority: providerAuthority
        ]

        buildConfigField 'String', 'PROVIDER_AUTHORITY', "\"$providerAuthority\""
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        density {
            enable true
            reset()
            include 'xhdpi'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    def leakcanaryVersion = '1.5'

    compile fileTree(dir: 'libs', include: '*.jar')
    compile "com.android.support:leanback-v17:$support_library_version"
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile('com.github.bumptech.glide:glide:4.0.0-SNAPSHOT')
    compile('com.github.bumptech.glide:okhttp-integration:2.0.0-SNAPSHOT')
    compile "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    compile "io.reactivex.rxjava2:rxjava:$rxjava_version"
    compile "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    annotationProcessor "com.github.gfx.android.orma:orma-processor:$orma_version"
    compile "com.github.gfx.android.orma:orma:$orma_version"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"

    [':library'].forEach{
        debugCompile project (path: it, configuration: 'debug')
        releaseCompile project (path: it, configuration: 'release')
    }
}
