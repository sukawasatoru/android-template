apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'net.ltgt.errorprone'
apply plugin: 'versioning'

def providerAuthority = app_id

android {
    defaultConfig {
        applicationId app_id
        versionCode 1
        versionName '0.A.1.0'

        manifestPlaceholders = [
                providerAuthority: providerAuthority
        ]

        buildConfigField 'String', 'PROVIDER_AUTHORITY', "\"$providerAuthority\""
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard = true
            shrinkResources = true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        density {
            enable = true
            reset()
            include 'xhdpi'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    applicationVariants.all {
        if (it.buildType.name == 'release') {
            it.outputs.all {
                versionCodeOverride = appVersion.versionCode
                versionNameOverride = appVersion.versionName
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation project(':library')
    implementation Depend.support.leanback
    implementation Depend.support.compatV7

    // for Orma
    implementation Depend.support.annotations
    implementation Depend.support.compat

    implementation Depend.glide.glide
    implementation Depend.glide.okhttp3
    implementation Depend.dagger.dagger
    annotationProcessor Depend.dagger.compiler
    implementation Depend.rx.java
    implementation Depend.rx.android
    implementation Depend.orma.orma
    annotationProcessor Depend.orma.compiler
    debugImplementation Depend.leakcanary.debug
    releaseImplementation Depend.leakcanary.release
    testImplementation Depend.leakcanary.test
    errorprone Depend.errorprone
}
