apply plugin: 'versioning'

buildscript {
    repositories {
        google()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

    dependencies {
        classpath Depend.gradle.android
        classpath Depend.gradle.kotlin
        classpath Depend.gradle.errorprone
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

def setupAndroidPlugin = { isCi, isApp, project ->
    project.android {
        compileSdkVersion Version.manifest.compileSdk
        buildToolsVersion Version.manifest.buildTools

        defaultConfig {
            if (isApp) {
                versionCode 1
                versionName '0.A.1.0'
            }

            minSdkVersion Version.manifest.minSdk
            targetSdkVersion Version.manifest.targetSdk
        }

        dexOptions {
            preDexLibraries = !isCi
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }

        if (isApp) {
            applicationVariants.all {
                if (it.buildType.name == 'release') {
                    it.outputs.all {
                        versionCodeOverride = appVersion.versionCode
                        versionNameOverride = appVersion.versionName
                    }
                }
            }
        }
    }
}.curry(System.getenv('CI') == 'true')

subprojects {
    project.plugins.whenPluginAdded {
        def isApp = it.class.name == 'com.android.build.gradle.AppPlugin'
        def isLib = it.class.name == 'com.android.build.gradle.LibraryPlugin'
        if (isApp || isLib) {
            setupAndroidPlugin(isApp, project)
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
