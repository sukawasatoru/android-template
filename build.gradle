buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
    }
}

plugins {
    id 'org.jetbrains.kotlin.android' version '1.3.72' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72' apply false
    id 'org.gradle.android.cache-fix' version '1.0.10' apply false
    id 'org.sonarqube' version '3.0'
}

sonarqube {
    properties {
        property 'sonar.android.lint.report', 'build/reports/lint-results.xml'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

def setupAndroidPlugin = { isCi, project ->
    project.android {
        compileSdkVersion 29
        buildToolsVersion '30.0.2'

        defaultConfig {
            minSdkVersion 24
            targetSdkVersion 29
        }

        dexOptions {
            preDexLibraries = !isCi
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }
}.curry(System.getenv('CI') == 'true')

def setupKotlinAndroidPlugin = { project ->
    project.android {
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
        }

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}

subprojects {
    project.plugins.whenPluginAdded {
        switch (it.class.name) {
            case 'com.android.build.gradle.AppPlugin':
            case 'com.android.build.gradle.LibraryPlugin':
                setupAndroidPlugin(project)
                break
            case 'org.jetbrains.kotlin.gradle.plugin.KotlinAndroidPluginWrapper':
                setupKotlinAndroidPlugin(project)
                break
            default:
                break
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
