buildscript {
    repositories {
        google()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

    dependencies {
        classpath Depends.gradle.android
        classpath Depends.gradle.kotlin
        classpath Depends.gradle.errorprone
    }
}

plugins {
    id 'org.sonarqube' version '2.6.2'
}

apply plugin: 'versioning'

sonarqube {
    properties {
        property 'sonar.android.lint.report', 'build/reports/lint-results.xml'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

def setupAndroidPlugin = { isCi, project ->
    project.android {
        compileSdkVersion Versions.manifest.compileSdk
        buildToolsVersion Versions.manifest.buildTools

        defaultConfig {
            minSdkVersion Versions.manifest.minSdk
            targetSdkVersion Versions.manifest.targetSdk
        }

        dexOptions {
            preDexLibraries = !isCi
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }
}.curry(System.getenv('CI') == 'true')

subprojects {
    project.plugins.whenPluginAdded {
        switch (it.class.name) {
            case 'com.android.build.gradle.AppPlugin':
            case 'com.android.build.gradle.LibraryPlugin':
                setupAndroidPlugin(project)
                break
            default:
                break
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
