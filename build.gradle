buildscript {
    repositories {
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.4'
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.42'
    }
}

plugins {
    id 'com.apollographql.apollo' version '2.5.11' apply false
    id 'com.diffplug.spotless' version '6.9.1' apply false
    id 'com.google.protobuf' version '0.8.18' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.10' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10' apply false
}

def setupAndroidPlugin = { Project project ->
    project.repositories {
        google()
        mavenCentral()

        maven {
            url 'https://maven.pkg.github.com/sukawasatoru/android-logger'
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    project.android {
        compileSdk 31
        buildToolsVersion '32.0.0'

        defaultConfig {
            minSdk 24
            targetSdk 31
        }

        compileOptions {
            coreLibraryDesugaringEnabled true
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        signingConfigs {
            config {
                enableV3Signing true
                enableV4Signing true
            }
        }
    }

    project.dependencies {
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    }
}

def setupKotlinAndroidPlugin = { Project project ->
    project.android {
        sourceSets.each {
            it.java.srcDirs += "src/${it.name}/kotlin"
        }

        kotlinOptions {
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
            jvmTarget = JavaVersion.VERSION_11
        }
    }
}

def setupSpotlessPlugin = { Project project ->
    project.spotless {
        kotlin {
            target project.fileTree(dir: 'src', include: '**/*.kt', exclude: ['test/**/*.kt', 'androidTest/**/*.kt'])
            ktlint('0.45.2').userData([
                android: 'true',
                'max-line-length': '120',
            ])
        }
    }
}

subprojects {
    plugins.whenPluginAdded {
        switch (it.class.name) {
            case 'com.android.build.gradle.AppPlugin':
            case 'com.android.build.gradle.LibraryPlugin':
                setupAndroidPlugin(project)
                break
            case 'org.jetbrains.kotlin.gradle.plugin.KotlinAndroidPluginWrapper':
                setupKotlinAndroidPlugin(project)
                break
            case 'com.diffplug.gradle.spotless.SpotlessPlugin':
                setupSpotlessPlugin(project)
                break
            default:
                break
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
